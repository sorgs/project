apply plugin: 'com.android.application'

apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.sorgs.app"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
    }
    signingConfigs {
        debug {
            storeFile file(DEBUG_STORE_FILE)
            storePassword DEBUG_STORE_PASSWORD
            keyAlias DEBUG_KEY_ALIAS
            keyPassword DEBUG_KEY_PASSWORD
        }
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            //Zipalign优化
            zipAlignEnabled false
            //去除无用资源
            shrinkResources false
            //签名
            signingConfig signingConfigs.release
            //混淆
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            //签名
            debuggable true
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        javaMaxHeapSize "8g" //此处可根据电脑本身配置 数值越大 当然越快
        preDexLibraries = false
    }

    applicationVariants.all {
        variant ->
            variant.outputs.all {
                def app_name = variant.applicationId
                outputFileName = "${variant.name}_${app_name.substring(app_name.lastIndexOf(".") + 1, app_name.length())}" +
                        "_v${variant.versionName}.apk"
            }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'

    debugImplementation project(':baseproject') //debug 版本直接引用本地项目
    releaseImplementation 'com.github.sorgs:project:v1.7'   //release 版本引用远程版本用来最终测试发现问题
    //butterknife
    implementation 'com.jakewharton:butterknife:9.0.0-rc3'
    annotationProcessor 'com.jakewharton:butterknife-compiler:9.0.0-rc3'


}
